# ALM with random study effects to include single-arm studies
# Have to use a model file in order to use the dnorm(,)I(,) syntax to truncate priors on sd in disconnected evidence

# The closest matching RCT must be matched externally and the 'mu' taken from an independent baselines model

# Data are same as TSD format: ns, nt, na, r, n, t
# The data on single arm studies are:
# ns.single : number of single arm studies
# r.single : number of events in single arm studies
# n.single : number of patients in single arm studies
# t.single : treatment in single arm study
# mu.plugin[] is the plugin estimator from RCT with index matched.rct[]

# sd.connected.mean and sd.connected.tau are the mean and precision of the sd in the connected components.
# These are used as informative priors on sd.disc in the random effects models


# Binomial likelihood, logit link 
# Plugin estimator to include single-arm studies
model{ # *** PROGRAM STARTS 
	# Model for RCTs ##############################################
	for(i in 1:ns){ # LOOP THROUGH STUDIES 
		w[i,1] <- 0 # adjustment for multi-arm trials is zero for control arm 
		delta[i,1] <- 0 # treatment effect is zero for control arm 
		mu[i] ~ dnorm(0,0.01) # Baseline are a nuisance for RCT evidence
		for (k in 1:na[i]) { # LOOP THROUGH ARMS 
			r[i,k] ~ dbin(p[i,k],n[i,k]) # binomial likelihood 
			logit(p[i,k]) <- mu[i] + delta[i,k] # model for linear predictor 
			rhat[i,k] <- p[i,k] * n[i,k] # expected value of the numerators 
			dev.NA[i,k] <- 2 * (r[i,k] * (log(r[i,k])-log(rhat[i,k])) #Deviance contribution including NAs 
			+ (n[i,k]-r[i,k]) * (log(n[i,k]-r[i,k]) - log(n[i,k]-rhat[i,k]))) 
			dev[i,k] <- dev.NA[i,k]*(1-equals(n[i,1],1)) # Deviance contribution with correction for NAs 
		} 
		resdev[i] <- sum(dev[i,1:na[i]]) # summed residual deviance contribution for this trial 
		for (k in 2:na[i]) { # LOOP THROUGH ARMS 
			delta[i,k] ~ dnorm(md[i,k],taud[i,k]) # trial-specific LOR distributions 
			md[i,k] <- d[t[i,k]] - d[t[i,1]] + sw[i,k] # mean of LOR distributions (with multi-arm trial correction) 
			taud[i,k] <- tau *2*(k-1)/k # precision of LOR distributions (with multi-arm trial correction) 
			w[i,k] <- (delta[i,k] - d[t[i,k]] + d[t[i,1]]) # adjustment for multi-arm RCTs 
			sw[i,k] <- sum(w[i,1:k-1])/(k-1) # cumulative adjustment for multi-arm trials 
		} 
	}

	# Model for single-arm studies #######################################
	for(i in 1:ns.single)
	{
		r.single[i]~dbin(p.single[i],n.single[i])
		mu.plugin[i]~dnorm(mu.plugin.mean[i],mu.plugin.prec[i])
		logit(p.single[i])<-mu.plugin[i]+delta.single[i]
		delta.single[i]~dnorm(md.single[i],tau.disc) # Treatment effect relative to reference
		md.single[i]<- d[t.single[i]] - d[t[matched.rct[i],1]]
		rhat.single[i] <- p.single[i] * n.single[i] # expected value of the numerators 
		dev.single[i] <- 2 * (r.single[i] * (log(r.single[i])-log(rhat.single[i])) 
		+ (n.single[i]-r.single[i]) * (log(n.single[i]-r.single[i]) - log(n.single[i]-rhat.single[i]))) #Deviance contribution 
	}

	# Calculate deviance ##############################################
	totresdev.single<-sum(dev.single[]) # Total residual deviance for single-arm studies
	totresdev.rct <- sum(resdev[]) # Total residual deviance for RCTs
	totresdev<-totresdev.single+totresdev.rct #Total Residual Deviance 
	
	# Specify remaining priors
	d[1]<-0 # treatment effect is zero for reference treatment 
	for (k in 2:nt){ d[k] ~ dnorm(0,.01) } # vague priors for treatment effects 
	sd ~ dunif(0,2) # vague prior for between-trial SD 
	tau <- pow(sd,-2) # between-trial precision = (1/between-trial variance) 

	# Priors for single-arm studies random study effects models
	sd.disc ~ dnorm(sd.connected.mean, sd.connected.tau)I(0,) # Informative prior based on connected SD
	tau.disc <- pow(sd.disc,-2) # between-trial precision = (1/between-trial variance) 
}
